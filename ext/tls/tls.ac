dnl
dnl Configure ext/tls
dnl This file is included by the toplevel configure.ac
dnl

dnl with-tls option
dnl
dnl   --with-tls=TYPE,TYPE,...    # include TLS library TYPE, ...
dnl   --with-tls=no               # do not include any TLS library support
dnl
dnl   We include all available (and supported) TLS libraries by defualt.
dnl   This option is to _exclude_ some or all libraries that would be
dnl   compiled otherwise.
dnl

TLSLIBS=axtls,mbedtls
AC_ARG_WITH(tls,
  AS_HELP_STRING([--with-tls=TLSLIB,...],
  [Select which TLS libraries to be compiled.  Currently we support 'axtls'
   (to use bundled souce of Cameron Rich's axTLS) and 'mbedtls' (to use mbed
   TLS).  By default, available libraries are autodetected.  Specify this
   option only when you want to exclude some of the libraries.
   Specify --with-tls=no to disable tls support.]),
  [
    AS_CASE([$with_tls],
      [no|none], [TLSLIBS=""],
      [yes],     [],            dnl Use the default
                 [TLSLIBS="$with_tls"])
  ])

AC_ARG_ENABLE(tls,
  AS_HELP_STRING([--enable-tls=TLSLIB,...],
  [Obsoleted.  Same as --with-tls=TLSLIB,... for the compatibility.]),
  [
    AC_MSG_WARN([--enable-tls is obsoleted.  Use --with-tls.])
    AS_CASE([$enableval],
      [no|none], [TLSLIBS=""],
      [yes],     [],            dnl Use the default
                 [TLSLIBS="$enableval"])
  ])

dnl Check for tls library availability.

GAUCHE_TLS_SWITCH_AXTLS="@%:@"
GAUCHE_TLS_SWITCH_AXTLS_TEST="@%:@"
GAUCHE_TLS_SWITCH_MBEDTLS="@%:@"
GAUCHE_TLS_SWITCH_NONE=

dnl We bundle axTLS so it's always available.
AS_IF([echo $TLSLIBS | tr "," "\012" | grep -q axtls], [
  AC_DEFINE(GAUCHE_USE_AXTLS, 1, [Define if you use axTLS])
  GAUCHE_TLS_SWITCH_AXTLS=
  GAUCHE_TLS_SWITCH_NONE="@%:@"
  GAUCHE_TLS_TYPES="axtls $GAUCHE_TLS_TYPES"
  dnl axtls test program requires thread support.  For the time being,
  dnl we turn off test program if the thread support is turned off
  dnl (which is not ideal).
  AS_IF([test "$GAUCHE_THREAD_TYPE" = 'none'], [
    GAUCHE_TLS_SWITCH_AXTLS_TEST="@%:@"
  ], [
    GAUCHE_TLS_SWITCH_AXTLS_TEST=
  ])
])

dnl mbedtls
AS_IF([echo $TLSLIBS | tr "," "\012" | grep -q mbedtls], [
  AC_CHECK_HEADER([mbedtls/net_sockets.h],
    [AC_DEFINE(HAVE_MBEDTLS_NET_SOCKETS_H, 1, 
               [Define to 1 if you have the <mbedtls/net_sockets.h> header file.])],
    [AC_CHECK_HEADER([mbedtls/net.h],
      [AC_DEFINE(HAVE_MBEDTLS_NET_H, 1, [Define to 1 if you have the <mbedtls/net.h> header file.])],
      [mbedtls_unavailable=yes])])

  dnl We don't want to include mbedtls libraris in generic LIBS
  LIBS_SAVE=${LIBS}
  MBEDTLS_LIBS=""
  AC_SEARCH_LIBS([mbedtls_ctr_drbg_init], [mbedcrypto],
   [MBEDTLS_LIBS="-lmbedcrypto ${MBEDTLS_LIBS}"],
   [mbedtls_unavailable=yes])
  AC_SEARCH_LIBS([mbedtls_x509_crt_init], [mbedx509], 
   [MBEDTLS_LIBS="-lmbedx509 ${MBEDTLS_LIBS}"],
   [mbedtls_unavailable=yes])
  AC_SEARCH_LIBS([mbedtls_ssl_init], [mbedtls], 
   [MBEDTLS_LIBS="-lmbedtls ${MBEDTLS_LIBS}"],
   [mbedtls_unavailable=yes])
  LIBS=${LIBS_SAVE}
  EXT_LIBS="$EXT_LIBS $MBEDTLS_LIBS"

  AS_IF([test "$mbedtls_unavailable" = yes], [
    AC_MSG_NOTICE([Can't find mbedtls headers and/or libraries.])
    ],[
      AC_DEFINE(GAUCHE_USE_MBEDTLS, 1, [Define if you use mbed TLS])
      AC_SUBST(MBEDTLS_LIBS)
      GAUCHE_TLS_TYPES="mbedtls $GAUCHE_TLS_TYPES"
      GAUCHE_TLS_SWITCH_MBEDTLS=
      GAUCHE_TLS_SWITCH_NONE="@%:@"
   ])
  ])

AC_SUBST(GAUCHE_TLS_SWITCH_AXTLS)
AC_SUBST(GAUCHE_TLS_SWITCH_AXTLS_TEST)
AC_SUBST(GAUCHE_TLS_SWITCH_MBEDTLS)
AC_SUBST(GAUCHE_TLS_SWITCH_NONE)

AC_ARG_WITH([ca-bundle],
  AS_HELP_STRING([--with-ca-bundle=/path/to/ca-bundle.crt],
		 [Specify the default CA certificate bundle file path for 
                  TLS certificate validation. This file is required to use 
                  mbed TLS.]),
  [
    AS_CASE([$with_ca_bundle],
	[yes|no], [AC_DEFINE([GAUCHE_CA_BUNDLE], ["ca-cert.crt"])],
                  [AC_DEFINE_UNQUOTED([GAUCHE_CA_BUNDLE], ["$with_ca_bundle"], [CA file path])]
     )
  ], [
    AC_DEFINE([GAUCHE_CA_BUNDLE], ["ca-cert.crt"])
  ])

dnl
dnl Check openssl command; if available, we use it for axTLS tests.
dnl This is needed even if we don't support libopenssl binding.
dnl
AC_CHECK_PROGS(OPENSSL, openssl)

dnl Local variables:
dnl mode: autoconf
dnl end:
